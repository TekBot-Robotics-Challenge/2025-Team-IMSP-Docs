# Partie √©lectronique

# Circuiterie

Cette section d√©crit l‚Äôensemble des composants √©lectroniques utilis√©s, leur r√¥le dans le fonctionnement du syst√®me, ainsi que le sch√©ma de c√¢blage g√©n√©ral.

1. **Le buzzer**

      Un **buzzer** est un petit composant √©lectronique qui √©met un son lorsqu‚Äôil est aliment√©. Il est utilis√© pour produire des signaux sonores, comme des bips ou des alarmes. Il en existe deux types principaux : le **buzzer actif**, qui produit un son d√®s qu‚Äôon lui applique une tension (g√©n√©ralement 5V), et le **buzzer passif**, qui a besoin d‚Äôun signal oscillant (comme un signal PWM) pour √©mettre un son.

![buzzer (2).png](./assets/buzzer_(2).png)

1. **La LED RGB**

    Une **LED RGB** (Red-Green-Blue) est une diode √©lectroluminescente capable d‚Äô√©mettre diff√©rentes couleurs en combinant trois LED de base : une rouge, une verte et une bleue, log√©es dans un m√™me bo√Ætier. Elle peut √™tre **√† cathode commune** (les trois LED partagent un m√™me GND) ou **√† anode commune** (elles partagent un m√™me +V).

![RGB LED.png](./assets/RGB_LED.png)

En faisant varier l‚Äôintensit√© de chaque couleur via des signaux PWM, on peut obtenir un large √©ventail de couleurs. Elle se connecte √† trois sorties PWM d‚Äôun microcontr√¥leur, permettant de cr√©er des effets lumineux dynamiques (comme des d√©grad√©s, clignotements ou transitions).

1. **Le driver A4988**

   Le **driver A4988** est un module con√ßu pour contr√¥ler facilement un **moteur pas √† pas bipolaire**. Il agit comme un pont en H double, capable de g√©rer le courant dans les deux bobines du moteur pour le faire tourner pas √† pas. Il permet aussi d‚Äôutiliser des **micro-pas** (jusqu‚Äô√† 1/16 de pas) pour un mouvement plus fluide et pr√©cis.

![Driver.png](./assets/Driver.png)

Le A4988 se commande avec seulement deux signaux : **STEP** (pour faire un pas) et **DIR** (pour choisir la direction). Il n√©cessite deux alimentations : une pour la **logique (3,3V ou 5V)** et une autre pour le **moteur (jusqu‚Äô√† 35V)**. Il est id√©al pour les imprimantes 3D, traceurs, ou tout projet n√©cessitant un contr√¥le pr√©cis de position.

**Pr√©sentation du sch√©ma de notre circuit**

   Pour ce 4√®me et dernier test de pr√©s√©lection, il nous √©tait demand√© de r√©aliser un convoyeur de d√©chets qui serait contr√¥l√© gr√¢ce au microprocesseur Atmega 328p comme lors des semaines pr√©c√©dentes. Donc, pour le faire, on a d‚Äôabord choisi notre microprocesseur directement disponible dans la biblioth√®que Kicad auquel on a associ√© son circuit d‚Äôoscillation, de Reset, la LED d‚ÄôEtat et un connecteur d‚Äôalimentation comme lors des semaines pr√©c√©dentes. Cela est fait suivant le sch√©ma suivant¬†:

![Sch√©ma Atmega .png](./assets/Schma_Atmega_.png)

Pour la d√©tection des d√©chets sur le tapis, on a choisi d‚Äôutiliser 2 √©metteurs laser du m√™me type, le KY-008. On l‚Äôa repr√©sent√© par un connecteur 3 pin, qui correspondent respectivement au signal, au VCC et au GND. Pour la broche du signal, on a choisi de cr√©er les labels S1 et S2 qui ont √©t√© connect√©es aux broches PD5 et PD6 de notre Atmega.

![Emetteurs laseer.png](./assets/Emetteurs_laseer.png)

**Sch√©ma de c√¢blage des √©metteurs laser KY-008**

   Pour recevoir l‚Äôinformation capt√©e par les √©metteurs, ces derniers sont fournis avec des modules r√©cepteurs qui sont l‚Äôassociation de photor√©sistances et de r√©sistances de 10k. Ils suivent le m√™me sch√©ma que les √©metteurs laser, c‚Äôest-√†-dire un connecteur 3 pin, mais avec les broches du signal qui ont √©t√© associ√©es aux labels R1 et R2 qui sont quant √† eux connect√©s respectivement aux broches PD2 et PD3 de notre microprocesseur. A la base, on ne voulait pas les inclure dans notre sch√©ma, mais les r√©cepteurs qui ont √©t√© livr√©s avec nos¬† √©metteurs laser √©tant d√©faillants, on a d√ª fabriquer nous-m√™mes nos propres r√©cepteurs suivant le mod√®le suivant¬†:

![Wokwi recepteurs.png](./assets/Wokwi_recepteurs.png)

Ce qui a conduit au sch√©ma Kicad suivant¬†:

![Kicad recepteurs 1.png](./assets/Kicad_recepteurs_1.png)

Dans notre sch√©ma final, on a d√©cid√© de repr√©senter nos 2 r√©cepteurs de la mani√®re suivante¬†:

![Kicad recepteurs final.png](./assets/Kicad_recepteurs_final.png)

**Sch√©ma de c√¢blage des r√©cepteurs laser**

Le montage physique de nos r√©cepteurs sera pr√©sent√© plus tard dans la documentation.

   Pour assurer la communication entre notre interface web et notre Atmega, on a choisi d‚Äôutiliser un module bluetooth, le HC05 qui est constitu√© de 6 broches. Mais comme pour notre circuit, seulement 4 broches seront utilis√©es donc on a choisi d‚Äôutiliser un connecteur 4 pin, les 2 premi√®res correspondant aux broches VCC et GND et les deux derni√®res seront connect√©es au microprocesseur. Il s‚Äôagit des broches RX et TX qui seront connect√©es respectivement aux broches TXD et RXD de l‚ÄôAtmega. Pour ce faire on a cr√©√© les labels RX et TX toujours dans l‚Äôoptique de faciliter la compr√©hension de notre circuit.

![Module HC05.png](./assets/Module_HC05.png)

**Sch√©ma de c√¢blage du module HC05**

   Pour notre capteur de couleur, on est partis sur le capteur TCS 230 et pour le repr√©senter dans notre logiciel, on a choisi d‚Äôutiliser un connecteur de deux rang√©es de 4 pin comme le module. Il y a 3 pin qui sont r√©serv√©es √† l‚Äôalimentation, une pin VCC et 2 GND. Pour le c√¢blage des autres broches, on a cr√©√© les labels OUT, S0, S1, S2 et S3 qui sont connect√©s respectivement aux broches PD2, PD4, PD5, PD6 et PD7 de l‚ÄôAtmega.

![Capteur de couleur kicad.png](./assets/Capteur_de_couleur_kicad.png)

**Sch√©ma de c√¢blage du capteur de couleur**

   Lorsque la d√©tection d‚Äôun d√©chet sur le tapis est faite, une led RGB qui est une association de 3 leds de couleurs diff√©rentes s‚Äôallume en fonction de la couleur du d√©chet sur le tapis. Donc la Led jaune pour un d√©chet de couleur jaune. Ce module est repr√©sent√© dans notre sch√©ma par un connecteur √† 4 pin, les 3 premi√®res repr√©sentant celles qui sont connect√©es √† l‚Äôatmega. Il s‚Äôagit des broches connect√©es aux labels LED1, LED2 et LED3 qui sont connect√©es respectivement aux broches PB1, PB2 et PB3 de notre microprocesseur.

![Kicad LED RGB.png](./assets/Kicad_LED_RGB.png)

**Sch√©ma de c√¢blage de la LED RGB**

   De plus, on a d√©cid√© d‚Äôajouter un buzzer qui sonnera √† chaque fois qu‚Äôun objet sera d√©tect√© sur le tapis. Sa broche r√©ceptrice de signal est mise sur la broche PD3 de notre Atmega gr√¢ce au label Signal buzzer.

![Kicad buzzer.png](./assets/Kicad_buzzer.png)

**Sch√©ma du c√¢blage du buzzer**

  Pour le contr√¥le de notre moteur, on est partis sur un driver A4988 qui poss√®de 8 broches dont les connexions sont les suivantes¬†:

Bobine A : 1A (A4988) <-> 1 fil moteur

- Bobine B : 2A (A4988) <-> 1 fil moteur

- STEP -> D3

- VDD -> 5V

- VMOT -> +12V

- Alimentation moteur :

- Bobine A : 1B (A4988) <-> 1 fil moteur

- GND -> GND

- DIR -> D4

- Bobine B : 2B (A4988) <-> 1 fil moteur

- A4988 vers microprocesseur :
- Connecter les 4 fils du moteur NEMA17 √† l‚ÄôA4988 :

- GND VMOT¬†->¬†GND¬†alim

Mais pour notre sch√©ma vu qu‚Äôil fallait juste connecter les broches STEP, DIR, VCC, GND on a choisi d‚Äôutiliser un connecteur 4 pin avec des broches du m√™me nom. Les broches STEP et DIR sont connect√©es respectivement aux broches PC0 et PC1 de notre Atmega avec des labels du m√™me nom.

![Kicad Driver.png](./assets/Kicad_Driver.png)

**Sch√©ma de c√¢blage du driver**

   Pour le circuit de notre driver on a choisi comme les r√©cepteurs de nos √©metteurs laser de faire le montage physique √† part .Donc le driver ne sera pas directement notre circuit final. Nous y reviendrons plus tard notre documentation.

![Kicad sch√©ma final.png](./assets/Kicad_schma_final.png)

**Sch√©ma final de notre circuit**

   L‚Äôex√©cution du contr√¥le des r√®gles √©lectriques de notre sch√©ma g√©n√®re le message suivant¬†:

![Ex√©cution ERC.png](./assets/Excution_ERC.png)

     Ce qui nous prouve que notre sch√©ma ne comporte pas des erreurs.

Voici un tableau r√©capitulatif des diff√©rentes connexions que l‚Äôon a √©ffectu√©:

| **Composant** | **Broche ATmega328P** | **R√©f√©rence Arduino** | **Num√©ro Arduino** |
| --- | --- | --- | --- |
| Signal 1er emetteur laser | PB0 (pin 14) | D8 | 8 |
| LED1 | PB1 (pin 15) | D9 | 9 |
| LED2 | PB2 (pin 16) | D10 | 10 |
| LED3 | PB3 (pin 17) | D11 | 11 |
| Signal 2√®me emetteur laser | PB5 (pin 19) | D13 | 13 |
| Oscillateur XTAL1 | PB6 (pin 9) | - | - |
| Oscillateur XTAL2 | PB7 (pin 10) | - | - |
| STEP (A4988) | PC0 (pin 23) | A0 | 14 |
| DIR (A4988) | PC1 (pin 24) | A1 | 15 |
| Signal du r√©cepteur 1er emetteur laser | PC2 (pin 25) | A2 | 16 |
| Signal du r√©cepteur 2√®me emetteur laser | PC3 (pin 26) | A3 | 17 |
| Reset | PC6 (pin 1) | Reset | - |
| Signal buzzer | PD3 (pin 5) | D3 | 3 |
| OUT (TCS230) | PD2 (pin 4) | D2 | 2 |
| S0 (TCS230) | PD4 (pin 6) | D4 | 4 |
| S1 (TCS230) | PD5 (pin 11) | D5 (PWM possible) | 5 |
| S2 (TCS230) | PD6 (pin 12) | D6 (PWM possible) | 6 |
| S3 (TCS230) | PD7 (pin 13) | D7 | 7 |

Apr√®s cela on est pass√©s au design du PCB et c‚Äôest toujours apr√®s un long travail de r√©arrangement des composants et des connexions qu‚Äôon a obtenu le PCB suivant¬†:

![PCB circuit principal.png](./assets/PCB_circuit_principal.png)

Et sa vue 3D¬†:

Il fait une dimension de 7x6cm et on a utilis√© majoritairement des pistes de longueur 1.5 mm mais aussi de 0.5mm.

[https://player.vimeo.com/video/1104738721?badge=0&amp;autopause=0&amp;player_id=0&amp;app_id=58479](https://player.vimeo.com/video/1104738721?badge=0&amp;autopause=0&amp;player_id=0&amp;app_id=58479)

On a √©galement fait le design du PCB de notre r√©cepteur¬†:

![PCB R√©cepetur .png](./assets/PCB_Rcepetur_.png)

Et sa vue 3D¬†:

[https://player.vimeo.com/video/1104739905?badge=0&amp;autopause=0&amp;player_id=0&amp;app_id=58479](https://player.vimeo.com/video/1104739905?badge=0&amp;autopause=0&amp;player_id=0&amp;app_id=58479)

Il fait 2.5x2 cm.

   Pour le montage de notre circuit principal, on a d√©cid√© de le r√©aliser sur un veroboard dont les images sont les suivantes¬†:

![**Vue arri√®re du circuit principal**](./assets/Vue_arriere_circuit_principal_veroboard.png)

**Vue arri√®re du circuit principal**

![**Vue avant du circuit principal**](./assets/Vue_avant_circuit_principal_veroboard.png)

**Vue avant du circuit principal**

Pour le montage du circuit des r√©cepteurs on a aussi d√©cid√© de les faire sur le veroboard.

![**Vue arri√®re du circuit du r√©cepteur** ](./assets/Vue_arrire_du_circuit_du_rcepteur_.png)

**Vue arri√®re du circuit du r√©cepteur** 

![**Vue avant du circuit du r√©cepteur** ](./assets/Vue_avant_circuit_rcepteur_.png)

**Vue avant du circuit du r√©cepteur** 

On a √©galement utilis√© la m√™me approche pour le driver de notre circuit ,c‚Äôest √† dire un montage sur veroboard mais vu qu‚Äôil allait s√ªrement √™tre r√©utilis√© pour un autre projet, on a d√©cid√© de souder des connecteurs sur notre veroboard et de faire la liaison avec jumpers.

![**Vue arri√®re du circuit du driver** ](./assets/Vue_arrire_circuit_driver_.jpg)

**Vue arri√®re du circuit du driver** 

Nous joyons √† notre documentation les diff√©rents fichiers Kicad de notre projet.

![**Vue avant du circuit du driver** ](./assets/Vue_avant_circuit_driver_.jpg)

**Vue avant du circuit du driver** 

# Syst√®me embarqu√©

# **D√©tection des d√©chets sur le tapis**

L‚Äôobjectif du travail fourni √† ce  niveau est de d√©tecter la pr√©sence d‚Äôun objet  sur le tapis.

Pour ce faire, on a d√©cid√© d‚Äôutiliser un **module laser diode KY-008** et comme r√©cepteur une **photor√©sistance**.

## **Module Laser diode KY-008**

![ModuleLaserKY-008.png](./assets/ModuleLaserKY-008.png)

Il s‚Äôagit d‚Äôun capteur compos√© principalement d‚Äôune LED , de dissipateurs thermiques et de lentilles convergentes. Ces lentilles permettent de converger les faisceaux lumineux provenant de la LED lorsqu‚Äôelle est allum√©e en un seul faisceau lumineux invisible √† l‚Äô≈ìil nu et de longueur d‚Äôonde **650 nm.** Au moins, ce qui est visible est le **point rouge** qui apparait lorsqu‚Äôil atteint une surface opaque.   

[KY-008, KY-008-AZ Datasheet PDF (AZ-Delivery) - Laser Transmitter module](https://datasheet4u.com/datasheet-pdf/AZ-Delivery/KY-008/pdf.php?id=1415012)

## **R√©cepteur: Photor√©sistance**

 Particuli√®rement au niveau du r√©cepteur, on voulait prendre le module KY-018 mais apr√®s quelques  tests avec lui, les donn√©es obtenues n‚Äô√©taient pas utilisables (valeurs nulles, variation faible des mesures) pour faire une d√©tection fiable .Donc on a d√©cid√© de prendre une photor√©sistance nue et de reproduire le circuit nous-m√™me. 

![Photoresistance.png](./assets/Photoresistance.png)

**photor√©sistance nue**

## **Principe de fonctionnement**

La photor√©sistance est une r√©sistance variable en fonction de la luminosit√© ambiante. Puisque c‚Äôest une r√©sistance variable, alors la donn√©e est continue et non binaire (0 ou 1) . Donc on comprend que les broches utilisables sont les pins analogiques(A0 √† A5). 

En fait , la luminosit√© est inversement proportionnelle √† la photor√©sistance. Donc:

- **plus il y a de la lumi√®re**, **plus faible est la photor√©sistance** et donc cette derni√®re laissera passer le courant plus facilement, **ce qui conduira √† une tension plus grande mesur√©e au niveau de la broche analogique** ;
- moins il y a de la lumi√®re , plus grande est la photor√©sistance et donc plus faible sera la tension mesur√©e.

Pour rappel, ses deux  broches sont connect√©es √† notre ATMega en rajoutant une r√©sistance **pull down (10kOhm)** de cette mani√®re :

```

       5V
        |
       [LDR]
        |
        +-------> A1 (entr√©e analogique Arduino)
        |
     [10 kŒ©]
        |
       GND

```

Pour avoir plus d‚Äôinformations par rapport √† la r√©sistance pull down, tapez sur le lien suivant : 

[**Pourquoi faut il rajouter un r√©sistance au circuit de la photor√©sistance et pourquoi dit on pull down?**](./subpages/resistance_utility.md)

## **D√©termination de la pr√©sence d‚Äôun objet sur le tapis en connaissant la luminosit√©**

- **Logique globale**

En pointant mon laser vers ma photor√©sistance, cette derni√®re mesure une tr√®s forte luminosit√©. Ainsi, on voit que si un objet se place devant le laser, il y aura une forte baisse de la luminosit√©. 

Puisque je n‚Äôai que deux √©tats √† distinguer(objet pr√©sent ou objet absent) alors il me suffit de fixer un **seuil** par rapport auquel je compare les valeurs de tension mesur√©e du signal . Si ces valeurs le d√©passent, alors la luminosit√© a augment√© et donc ‚Äúobjet absent‚Äù et le contraire pour ‚Äúobjet pr√©sent‚Äù.

- **Analyse des valeurs pour d√©termination du seuil**

Donc on a fait des mesures et on s‚Äôest rendu compte que lorsque qu‚Äôil n‚Äôy a pas d‚Äôobjet , les valeurs d√©passent 900 et le contraire lorsqu‚Äôil y en a. Il serait suffisant de prendre pour seuil **900**.

**Code-test de d√©tection sur le tapis**

```cpp
 // broche de connexion module laser
#define LASER 11
 // broche de connexion buzzer  
#define BUZZER 12
// broche de connexion capteur de lumi√®re
#define photoResistance A0    

int seuil=900;
void setup() {
  Serial.begin(9600);
    /*Configuration des broches */
  pinMode(LASER, OUTPUT); // en sortie 
  pinMode(BUZZER, OUTPUT); // en sortie
  pinMode(photoResistance,INPUT); // en entree
  digitalWrite(LASER,HIGH); //allumage du laser
  delay(500);
}

void loop() {
  // mesure de la lumi√®re √† l'aide d'un capteur
  int mesure= analogRead(photoResistance);
  Serial.print("Capteur de lumi√®re: " + String(mesure));

  
  if(mesureActu<=900) // Objet sur le tapis
  {
      tone(BUZZER, 500);  // activer le buzzer
  }
  else {noTone(BUZZER);} // desactivation du buzzer
  delay(500);
}

```

- **Circuit de test**

![CircuitLaserRecepteur.png](./assets/CircuitLaserRecepteur.png)

**Vid√©o de test**

[https://player.vimeo.com/video/1100859641?h=971be1dd69](https://player.vimeo.com/video/1100859641?h=971be1dd69)

## **Am√©lioration pour le test de fonctionnement du convoyeur**

Donc nous venons d‚Äôatteindre notre objectif : nous avons pu d√©tecter si un objet est pr√©sent sur le tapis ou non.  

Mais imaginons que plusieurs d√©chets soient d√©pos√©s sur le tapis, et comme on sait que lorsqu‚Äôon est √† la fin du convoyeur la bande s‚Äôarr√™te , ca veut dire que les d√©chets qui auraient √©t√© d√©pos√©s pendant le cycle du premier d√©chet seraient toujours sur la bande alors que cette derni√®re se serait d√©j√† arr√™t√©e . **Probl√©matique !!!** 

Donc il nous faut le nombre de d√©chets sur le tapis et on peut le faire notre d√©tecteur laser en d√©but du convoyeur. Le seul petit d√©tail, c‚Äôest qu‚Äôil ne faudrait pas qu‚Äôun m√™me d√©chet soit compt√© plusieurs fois et pour ce faire , on peut se baser sur la variation de la luminosit√©. 

Plus clairement, si un objet reste au niveau du laser, alors la variation des mesures sera faible (+-5). Et on est s√ªr que si un nouveau d√©chet est pos√© sur le tapis, la diff√©rence entre la valeur pr√©c√©dente et celle actuelle sera positive (car l‚Äôabsence d‚Äôobjet augmente la luminosit√© alors que la la pr√©sence la diminue).

**Code-test am√©lior√© de d√©tection sur le tapis**

```cpp
// broche de connexion module laser
#define LASER 11   
// broche de connexion buzzer
#define BUZZER 12 
 // broche de connexion capteur de lumi√®re
#define LDR A0   

int seuil=900;
int compteur=0;
int mesurePrecedente=0;
bool newObject=0;
void setup() {
  Serial.begin(9600);

  pinMode(LASER, OUTPUT);
  pinMode(BUZZER, OUTPUT);
  pinMode(LDR, INPUT);
  digitalWrite(LASER,HIGH);
  mesurePrecedente=analogRead(LDR);
  delay(500);
}

void loop() {
  // mesure de la lumi√®re √† l'aide d'un capteur
  int mesureActu = analogRead(LDR);
  Serial.print("Capteur de lumi√®re: " + String(mesureActu));

  newObject=newObjectOnTape(mesurePrecedente, mesureActu);
  if(mesureActu<=900 && newObject==1) //Nouvel Objet sur le tapis
  {
      tone(BUZZER, 500);  // activer le buzzer
      compteur++;
  }
  else {noTone(BUZZER);}
  Serial.println(" Compteur: "+String(compteur));
  mesurePrecedente=mesureActu;
  delay(500);
}

bool newObjectOnTape(int mesurePrecedente,int mesureActuelle)
{
if(mesurePrecedente-mesureActuelle>=100) return 1;// Il y a bien un nouvelle objet sur le tapis
return 0;
}

```

**R√©f√©rences et ressources utiles**

Voici une liste des ressources qui ont √©t√© consult√©es pour ce projet :

- [Arduino France - Module diode laser](https://arduino-france.site/module-diode-laser/) - Documentation sur l'utilisation des modules laser avec Arduino
- [Cha√Æne YouTube @openprogramming](https://www.youtube.com/channel/UCf4jGfp-BFp6GLd6eTptVMg) - Tutoriels vid√©o sur la programmation Arduino

# **Reconnaissance d‚Äôun d√©chet et de son type par un capteur de couleur**

L‚Äôobjectif du travail effectu√© est de reconnaitre un d√©chet en d√©terminant si sa couleur est Rouge, Vert, Bleu ou Jaune. 

## **Capteur de couleur TCS230**

Le capteur de couleur **TCS230** (ou sa version plus r√©cente **TCS3200**) est un capteur optique capable de d√©tecter la **couleur** d‚Äôun objet. Il repose sur un principe simple mais ing√©nieux m√™lant **filtrage de couleur** et **conversion lumi√®re ‚Üí fr√©quence**. Voici une explication claire et directe :

![capteurCouleur.png](./assets/capteurCouleur.png)

## Principe de fonctionnement du capteur TCS230

### 1. **Structure du capteur**

Le TCS230 contient :

- Une **matrice de photodiodes** (8√ó8 = 64 photodiodes ) sensibles √† la lumi√®re.

Pour en savoir bri√®vement par rapport aux photodiodes, tapez ce lien:

[**C‚Äôest quoi une photodiode?**(2)](./subpages/photodiode.md)

- Chaque photodiode est recouverte d‚Äôun **filtre** :
    - 16 d√©tectent le **rouge (R)**,
    - 16 le **vert (G)**,
    - 16 le **bleu (B)**,
    - 16 sont **sans filtre** (utilis√©s pour la luminosit√© globale).
- Une **logique de commutation** permet de s√©lectionner quel groupe de photodiodes est actif (R, G, B ou clair).
- Un **convertisseur courant‚Äìfr√©quence** transforme l‚Äôintensit√© lumineuse re√ßue par les photodiodes en un **signal carr√© en sortie** dont la **fr√©quence est inversement proportionnelle √† l‚Äôintensit√© lumineuse** d√©tect√©e.

![SchemaExplicatifCouleur.png](./assets/SchemaExplicatifCouleur.png)

### 2. **Fonctionnement √©tape par √©tape**

1. **√âclairage de l‚Äôobjet** : On √©claire l‚Äôobjet avec une lumi√®re blanche (souvent par des LED int√©gr√©es).
2. **R√©flexion** : L‚Äôobjet r√©fl√©chit une certaine quantit√© de lumi√®re rouge, verte et bleue en fonction de sa couleur.
3. **Filtrage** : On active successivement les filtres R, G, puis B :
    - Quand le filtre rouge est actif, seules les photodiodes rouges ¬´ mesurent ¬ª la lumi√®re rouge r√©fl√©chie.
    - Pareil pour vert et bleu.
4. **Conversion** : Le capteur convertit l‚Äôintensit√© re√ßue (R, G ou B) en une fr√©quence en sortie (`OUT`) :
    - Plus la couleur est intense, plus la fr√©quence est faible.

### R√©sum√©

| √âl√©ment | R√¥le |
| --- | --- |
| Photodiodes filtr√©es | Mesurent s√©par√©ment les composantes R, G, B |
| Convertisseur courant-fr√©quence | Transforme la lumi√®re d√©tect√©e en fr√©quence |
| Sortie `OUT` | Donne un signal carr√© dont la fr√©quence d√©pend de l‚Äôintensit√© de lumi√®re |
| Contr√¥le `S2` / `S3` | S√©lectionne les filtres R, G, B ou clair |
| Contr√¥le `S0` / `S1` | D√©termine le **facteur de division de fr√©quence** pour ajuster la vitesse de sortie |

Ici, vous avez une explication plus claire par rapport √† la fonction de S0/S1:

[**S0/S1, qu‚Äôest ce que c‚Äôest?**](./subpages/s0_s1.md)

## **D√©termination de la couleur d‚Äôun objet**

Pour pouvoir notre objectif, nous avons suivi un processus de r√©flexion que nous expliquons de mani√®re assez claire dans la page suivante avec les difficult√©s rencontr√©es. 

[Processus de r√©flexion et difficult√©s](./subpages/reflexion_process.md)

**Logique globale de l‚Äôid√©e finale** 

<aside>
üí°

Ici, l'id√©e est de mapper les valeurs de fr√©quences mesur√©es avec celles du code d√©cimal RGB (0 √† 255) et ensuite de mesurer la distance euclidienne entre les valeurs mesur√©es RGB et celles RGB correspondant √† chaque couleur √† d√©tecter avec en plus la couleur du tapis qui est noir. Et donc, la couleur pour laquelle la distance est la plus petite est la couleur de l'objet .

</aside>

- Pour faire ce mappage, il a fallu d'abord, **d√©terminer le min et le max des valeurs de fr√©quence**, pour ensuite faire le mappage. Le min a √©t√© mesur√© √† partir du blanc pur, et le max a √©t√© mesur√© √† partir du noir pur. Ce qui est assez compr√©hensible, puisque le noir, √©tant donn√© qu'il absorbe compl√®tement la lumi√®re alors les fr√©quences mesur√©es seront les plus grandes . Et le blanc, puisque c'est la couleur qui contient le maximum de toutes les couleurs (255,255,255), alors forc√©ment, ses fr√©quences seront les plus basses.

Il est important de pr√©ciser que pour plus de pr√©cision, on a consid√©r√© les valeurs de 0 √† 1023 au lieu de la plage RGB connue (0 √† 255) et que la distance entre le capteur et l‚Äôobjet en dessous de ce dernier est fixe(2.5cm).

Pour v√©rifier visuellement que la d√©tection est bonne, on utilise la LED RGB .

Voici le code de test: 

**Code-test d‚Äôidentification de la couleur**

```cpp
//Broches de connexions du capteur de couleur
#define S0 2
#define S1 3
#define S2 4
#define S3 5
#define sensorOut 6
//Broches de connexion de la led RGB 
#define BLU 9
#define GRN 10
#define RED 11

float redFreq, greenFreq, blueFreq;// Variables de fr√©quences 

 // Liste de couleurs de r√©f√©rence (RGB)
char*noms[]={"Rouge","Vert","Bleu", "Jaune","Aucune"};

const int refR[5]={1023,0,0,1023, 0}; //Rouge
const int refV[5]={0,1023,0,1023, 0}; //Vert
const int refB[5]={0, 0,1023,0, 0}; //Bleu

// Valeurs mesur√©es durant l'√©talonnage
const float minR = 29;  // Frequence de Rouge dans du blanc pur
const float maxR = 230;  // Frequence de Rouge dans du noir pur

const float minG = 30; // Frequence de Vert dans du blanc pur
const float maxG = 290;// Frequence de Vert dans du noir pur

const float minB = 25; // Frequence de Bleu dans du blanc pur
const float maxB = 230; // Frequence de Bleu dans du noir pur

void setup() {
  Serial.begin(9600);
  pinMode(S0, OUTPUT);
  pinMode(S1, OUTPUT);
  pinMode(S2, OUTPUT);
  pinMode(S3, OUTPUT);
  pinMode(RED, OUTPUT);
  pinMode(GRN, OUTPUT);
  pinMode(BLU, OUTPUT);
  pinMode(sensorOut, INPUT);
  //Fr√©quence √† 20%
  digitalWrite(S0, HIGH);
  digitalWrite(S1, LOW);
  }
  void loop() {
  // Lecture rouge
  digitalWrite(S2, LOW);
  digitalWrite(S3, LOW);
  redFreq = pulseIn(sensorOut,LOW);
  // Lecture vert
  digitalWrite(S2, HIGH);
  digitalWrite(S3, HIGH);
  greenFreq = pulseIn(sensorOut, LOW);
  // Lecture bleu
  digitalWrite(S2, LOW);
  digitalWrite(S3, HIGH);
  blueFreq = pulseIn(sensorOut,LOW);
  //Affichage des valeurs de fr√©quences lues
  Serial.print("Mesur√© Redfreq VertFreq BlueFreq = ");
  Serial.print(redFreq); Serial.print(", ");
  Serial.print(greenFreq); Serial.print(", ");
  Serial.println(blueFreq);

//Contraindre les valeurs de frequences √† rester dans la plage de valeurs d√©finies
  redFreq=constrain(redFreq,minR,maxR);
  greenFreq=constrain(greenFreq,minG,maxG);
  blueFreq=constrain(blueFreq,minB,maxB);
  
//Mappage des valeurs de fr√©quences et des valeurs RGB √©largies
  int R = map(redFreq, minR, maxR, 1023, 0);
  int G = map(greenFreq,minG, maxG, 1023, 0);
  int B = map(blueFreq, minB, maxB, 1023, 0);
  
// Affichage des valeurs de fr√©quences mapp√©es
  Serial.print("Mesur√© RGB = ");
  Serial.print(R); Serial.print(", ");
  Serial.print(G); Serial.print(", ");
  Serial.println(B);
  
 //Determination de l'indice de la couleur avec la distance minimale 
int indexCouleur=compareCouleur(R,G,B);

//Affichage de la couleur d√©tect√©e
Serial.print("Couleur d√©tect√©e : ");

//Allumage de la LED RGB en la couleur correspondante
afficherLED(indexCouleur);
Serial.println(noms[indexCouleur]);
Serial.println("--------------------------");
  delay(1000);
}

float couleurDistance(int R,int G,int B,int r1,int r2,int r3)
{
  return sqrt(pow(r1-R,2)+pow(r2-G,2)+pow(r3-B,2));
}

int compareCouleur(int R, int G, int B)
{
  int i=0;
  int distance=0;
  int minDistance=couleurDistance(refR[0],refV[0],refB[0],R,G,B); //calcul de la distance de la mesure actuelle √† la valeur de reference du rouge
  int indexMin= 0;
  for (i=1;i<5;i++)
  {
    distance=couleurDistance(refR[i],refV[i],refB[i],R,G,B);
    if(minDistance>=distance)
    {
      minDistance=distance;
      indexMin=i;
    }
    
  }
  return indexMin;
}

void afficherLED(int n)
{
  if (n==0) //Rouge 
  { analogWrite(RED, 255);
    analogWrite(GRN, 0);
    analogWrite(BLU, 0);
  }
  else if(n==1)//Vert
  {
    analogWrite(RED, 0);
    analogWrite(GRN, 255);
    analogWrite(BLU, 0);
  }
  else if(n==2)// Bleu
  {
    analogWrite(RED, 0);
    analogWrite(GRN, 0);
    analogWrite(BLU, 255);
  }
  else if(n==3)// Jaune
  {
    analogWrite(RED, 255);
    analogWrite(GRN, 255);
    analogWrite(BLU, 0); 
  }
  else //Aucune couleur
  {
  analogWrite(RED, 0);
  analogWrite(GRN, 0);
  analogWrite(BLU, 0); 
  }
}

```

<aside>
üí°

**Lien de quelques sites** 

[https://www.moussasoft.com/tcs230-capteur-de-couleur-avec-arduino](https://www.moussasoft.com/tcs230-capteur-de-couleur-avec-arduino/)/

[https://lastminuteengineers.com/tcs230-tcs3200-color-sensor-arduino-tutorial/](https://lastminuteengineers.com/tcs230-tcs3200-color-sensor-arduino-tutorial/)

[https://projecthub.arduino.cc/SurtrTech/color-detection-using-tcs3200230-a1e463](https://projecthub.arduino.cc/SurtrTech/color-detection-using-tcs3200230-a1e463)

[https://www.ic-components.fr/blog/TCS3200-Color-Sensor-Specifications-Arduino-Guide-How-It-Works.jsp](https://www.ic-components.fr/blog/TCS3200-Color-Sensor-Specifications-Arduino-Guide-How-It-Works.jsp)

[https://www.bing.com/ck/a?!&&p=e1349f6fe6e6ed9708f930acb23d776e2c1576476a5b23efac725f103513ca4aJmltdHM9MTc1MjE5MjAwMA&ptn=3&ver=2&hsh=4&fclid=13ca58cd-0a8e-6cc3-350b-4c7d0b496dfe&psq=tableau+rgb&u=a1aHR0cHM6Ly93d3cucmFwaWR0YWJsZXMub3JnL2ZyL3dlYi9jb2xvci9SR0JfQ29sb3IuaHRtbA&ntb=1](https://www.bing.com/ck/a?!&&p=e1349f6fe6e6ed9708f930acb23d776e2c1576476a5b23efac725f103513ca4aJmltdHM9MTc1MjE5MjAwMA&ptn=3&ver=2&hsh=4&fclid=13ca58cd-0a8e-6cc3-350b-4c7d0b496dfe&psq=tableau+rgb&u=a1aHR0cHM6Ly93d3cucmFwaWR0YWJsZXMub3JnL2ZyL3dlYi9jb2xvci9SR0JfQ29sb3IuaHRtbA&ntb=1)

</aside>

# **Contr√¥le de la vitesse du Moteur pas √† pas**

 **Alimentation et stabilisation :**

 Lors des premiers essais, un condensateur de 16 ¬µF utilis√© pour stabiliser l‚Äôalimentation sous 12 V a explos√©. Apr√®s remplacement par un condensateur de 1000 ¬µF, le circuit s‚Äôest comport√© correctement, assurant ainsi une alimentation stable et fiable pour le microcontr√¥leur et les moteurs.

 **Contr√¥le du moteur**

Cette partie concerne la gestion du moteur pas √† pas utilis√© pour entra√Æner le tapis roulant.

Objectif :
Faire tourner le moteur √† une vitesse contr√¥l√©e, suffisante pour convoyer les d√©chets sans qu‚Äôils soient projet√©s √† l‚Äôarr√™t.

√âtapes r√©alis√©es :

1. Calcul de la vitesse maximale :
    - Utilisation du principe d‚Äôinertie et du frottement pour d√©terminer la vitesse limite.
    - Conversion en nombre de pas par seconde ‚Üí 5500 pas/s th√©orique.
2. Test moteur :
    - Le moteur supporte jusqu‚Äô√† 1230 pas/s, ce qui reste suffisant pour un arr√™t sans propulsion.
3. Contr√¥le par code :
    - Plusieurs programmes test√©s pour g√©rer la vitesse.
    - Tentative de contr√¥le via potentiom√®tre (√©chec temporaire)
    
     **Pour plus d‚Äôinfos, voir la page [Contr√¥le moteur]**
    
    **.[https://www.notion.so/238bd6fe277b808bba63e6d112a00667?source=copy_link](https://www.notion.so/238bd6fe277b808bba63e6d112a00667?pvs=21)**
    

---

# **Communication Interface Web et ATMega: Module Bluetooth HC-05**

La communication entre l‚Äôinterface Web et l‚ÄôATMega est l‚Äôune des parties les plus importantes qui exige un module physique permettant l‚Äôenvoie de donn√©es sous un format utilisable de l‚ÄôATMega au backend. Pour ce faire , on a choisi le module Bluetooth HC-05 et le format Json. 

## **Module Bluetooth**

### **Pourquoi le Bluetooth?**

L'utilisation d'un module **Bluetooth** comme le **HC-05** avec un Arduino permet une **communication sans fil** entre l‚ÄôArduino et un autre appareil (PC, smartphone, tablette, etc.).

![Bluetooth.png](./assets/Bluetooth.png)

**Avantages du Bluetooth :**

- **Sans fil** : pas besoin de connexion physique, utile pour les syst√®mes embarqu√©s ou mobiles.
- **Facile √† mettre en ≈ìuvre** : le module HC-05 communique via UART (s√©rie), d√©j√† disponible sur l‚ÄôArduino.
- **Large compatibilit√©** : fonctionne avec des applications Android, des logiciels PC, ou des programmes Python/Flask via un port COM.
- **Port√©e suffisante** : environ 10 m√®tres (classe 2).
- **√âconomie d‚Äô√©nergie** : utile dans les projets aliment√©s par batterie.

### **Formatage Json des donn√©es: biblioth√®que**‚Äú**`ArduinoJson.h`**‚Äù

Le **JSON** (*JavaScript Object Notation*) est un format l√©ger et facile √† lire pour structurer et √©changer des donn√©es. Il est largement utilis√© dans les applications web pour transmettre des informations entre un client (navigateur) et un serveur. JSON est bas√© sur une syntaxe simple et intuitive, inspir√©e des objets en JavaScript, mais il est ind√©pendant du langage de programmation.

### Structure de base du JSON :

1. **Objets** : Une collection de paires cl√© valeur, entour√©e d'accolades `{}`.
2. **Tableaux** : Une liste ordonn√©e de valeurs, entour√©e de crochets `[]`.
3. **Types de donn√©es** :
    - Cha√Ænes de caract√®res (entre guillemets doubles : `"texte"`)
    - Nombres (entiers ou d√©cimaux)
    - Bool√©ens (`true` ou `false`)
    - Null (`null`)
    - Objets ou tableaux imbriqu√©s.

### Exemple simple :

```json
{
  "nom": "Alice",
  "age": 25,
  "estEtudiant": true,
  "competences": ["programmation", "design", "communication"],
  "adresse": {
    "ville": "Cotonou",
    "pays": "B√©nin"
  }
}
```

### Avantages :

- **Lisibilit√©** : Facile √† comprendre;
- **Compatibilit√©** : Pris en charge par presque tous les langages de programmation.
- **L√©ger** : Id√©al pour les √©changes rapides de donn√©es.

**Code-test d‚Äôenvoie de donn√©es par Bluetooth √† l‚Äôinterface Web**

```cpp
//librairie pour le formatage des donnees en Json
#include <ArduinoJson.h> 

//Broches de connexion pour le capteur de couleur
#define S0 2
#define S1 3
#define S2 4
#define S3 5
#define sensorOut 6

//Led RGB
#define BLU 9
#define GRN 10
#define RED 11

float redFreq, greenFreq, blueFreq;//fr√©quences

 // Liste de couleurs de r√©f√©rence (RGB) √©largies
char*noms[]={"Rouge","Vert","Bleu", "Jaune","Aucune"};
int couleurPrecedente=4;
const int refR[5]={1023,0,0,1023, 0}; //Rouge
const int refV[5]={0,1023,0,1023, 0}; //Vert
const int refB[5]={0, 0,1023,0, 0}; //Bleu

// Valeurs mesur√©es durant l'√©talonnage
const float minR = 29;  // Frequence de Rouge dans du blanc pur
const float maxR = 230;  // Frequence de Rouge dans du noir pur

const float minG = 30; // Frequence de Vert dans du blanc pur
const float maxG = 290;// Frequence de Vert dans du noir pur

const float minB = 25; // Frequence de Bleu dans du blanc pur
const float maxB = 230; // Frequence de Bleu dans du noir pur

void setup() {
  Serial.begin(9600);
  pinMode(S0, OUTPUT);
  pinMode(S1, OUTPUT);
  pinMode(S2, OUTPUT);
  pinMode(S3, OUTPUT);
  pinMode(RED, OUTPUT);
  pinMode(GRN, OUTPUT);
  pinMode(BLU, OUTPUT);
  pinMode(sensorOut, INPUT);
  // Fr√©quence √† 20%
  digitalWrite(S0, HIGH);
  digitalWrite(S1, LOW);
  
}

void loop() {
  // Lecture rouge
  digitalWrite(S2, LOW);
  digitalWrite(S3, LOW);
  redFreq = pulseIn(sensorOut,LOW);
  // Lecture vert
  digitalWrite(S2, HIGH);
  digitalWrite(S3, HIGH);
  greenFreq = pulseIn(sensorOut, LOW);
  // Lecture bleu
  digitalWrite(S2, LOW);
  digitalWrite(S3, HIGH);
  blueFreq = pulseIn(sensorOut,LOW);

  JsonDocument doc;// cr√©ation d'une variable de type Json

  redFreq=constrain(redFreq,minR,maxR);
  greenFreq=constrain(greenFreq,minG,maxG);
  blueFreq=constrain(blueFreq,minB,maxB);

  int R = map(redFreq, minR, maxR, 1023, 0);
  int G = map(greenFreq,minG, maxG, 1023, 0);
  int B = map(blueFreq, minB, maxB, 1023, 0);

int indexCouleur=compareCouleur(R,G,B);
if(indexCouleur!=4 && indexCouleur!=couleurPrecedente) //Couleur Detect√©
{
  if(indexCouleur==0)//Rouge
  {
    doc["color_waste"]="red";
  }
  else if (indexCouleur==1)//Vert
  {
    doc["color_waste"]="green";
  }
  else if(indexCouleur==2)//Bleu
  {
    doc["color_waste"]="blue";
  }
  else //Jaune
  {
    doc["color_waste"]="yellow";
  }
  //Envoie de donnees sous format Json
  serializeJson(doc, Serial);
  Serial.println(); 
}
couleurPrecedente=indexCouleur;

afficherLED(indexCouleur);
delay(100);
}

int couleurDistance(int R,int G,int B,int r1,int r2,int r3)
{
  return sqrt(pow(r1-R,2)+pow(r2-G,2)+pow(r3-B,2));
}
int compareCouleur(int R, int G, int B)
{
  int i=0, distance=0,indexMin= 0;
  int minDistance=couleurDistance(refR[0],refV[0],refB[0],R,G,B); //calcul de la distance de la mesure actuelle √† la valeur de reference du rouge
  for (i=1;i<5;i++)
  {
    distance=couleurDistance(refR[i],refV[i],refB[i],R,G,B);
    if(minDistance>=distance)
    {
      minDistance=distance;
      indexMin=i;
    }
    
  }
  return indexMin;
}

void afficherLED(int n)
{
  if (n==0) //Rouge 
  { analogWrite(RED, 255);
    analogWrite(GRN, 0);
    analogWrite(BLU, 0);
  }
  else if(n==1)//Vert
  {
    analogWrite(RED, 0);
    analogWrite(GRN, 255);
    analogWrite(BLU, 0);
  }
  else if(n==2)// Bleu
  {
    analogWrite(RED, 0);
    analogWrite(GRN, 0);
    analogWrite(BLU, 255);
  }
  else if(n==3)// Jaune
  {
    analogWrite(RED, 255);
    analogWrite(GRN, 255);
    analogWrite(BLU, 0); 
  }
  else //Aucune couleur
  {
  analogWrite(RED, 0);
  analogWrite(GRN, 0);
  analogWrite(BLU, 0); 
  }
}

```

## ‚ö†Ô∏è Attention au t√©l√©versement

Tu **dois d√©brancher le HC-05 pendant le t√©l√©versement du code**, car l‚ÄôIDE Arduino utilise aussi RX/TX pour uploader.

Sinon vous aurez cette erreur:

![ErreurUploadBluetooth-min.png](./assets/ErreurUploadBluetooth-min.png)

<aside>
üí°

**Lien de quelques sites**

[https://arduinojson.org/](https://arduinojson.org/)

</aside>

# **Code du syst√®me entier**

Apr√®s avoir divis√© le travail en sous partie, il a fallu lier ces sous parties en suivant une logique donn√©e. 

Notre code complet suit la logique suivante: 

- Lorsqu‚Äôun **d√©chet** est pos√© sur le tapis, le moteur s‚Äôactive et d√©place le d√©chet pos√© sur le tapis gr√¢ce au premier capteur laser avec une incr√©mentation de mon compteur puisqu‚Äôun nouveau d√©chet a √©t√© d√©tect√© sur le tapis.
- Tant que le capteur laser pos√© √† la fin n‚Äôa pas d√©tect√© un d√©chet, le moteur continue de tourner avec une vitesse constante.
- D√®s que le d√©chet est d√©tect√© par notre deuxi√®me capteur laser , il y a arr√™t du moteur et d√©tection de la couleur par le capteur de couleur avec envoie de donn√©es √† l‚Äôinterface Web.
- L‚Äôobjet sera toujours devant le laser en fin du tapis tant que sa couleur n‚Äôest pas d√©tect√© .
- Il est important de pr√©ciser que tant qu‚Äôil y aura de d√©chets sur le tapis, le moteur se r√©activera pour permettre aux d√©chets restants d‚Äô√™tre √©vacu√©s.

Nous avons partitionn√© le code final en plusieurs sous codes pour faciliter la maintenance et la lecture du code. 

### Codes finaux

**Code principal**

```arduino
/* Inclusion des biblioth√®ques */

//Contr√¥le des moteurs pas √† pas
#include <AccelStepper.h> 
 // Formatage  Json des donnees √† envoyer
#include <ArduinoJson.h>
// Allumage Led 
#include "LedRGB.h" 
//Detection de la couleur
#include "DetectionCouleur.h" 
//Detection de dechets sur le tapis
#include "DetectionSurBande.h"

// Broche de connexion du Driver donnant le pas 
#define pinStep A0 
// Broche de connexion du Driver donnant la direction 
#define pinDir A1 

/* Creation d'un driver de moteur */
AccelStepper stepper(1,pinStep,pinDir);
/* Nombre de pas du moteur par seconde */
const float vitt=300;
/* Nombre de dechets sur le tapis */
int compteur=0;

/* la mesure pr√©c√©dente du premier capteur laser */
int mesurePrecedente=0;

void setup() {
  /* Initialisation de la communication s√©rie */
   Serial.begin(9600);

  /* Configuration des broches utilis√©es pour la detection sur la bande */
  pinModeDetectSurBande();

  /* Configuration des broches utilis√©es pour la LED RGB */
  pinModeLedRgb(); 

  /* Configuration des broches utilis√©es pour la detection de couleur */
  pinModeDetectCouleur();

  /* Allumage des lasers */ 
  allumageLaser();

  /*Configuration de la vitesse du moteur */
  configureVitesseMoteur();

  /* Fr√©quence √† 20% */
  frequence(HIGH,LOW);

  /* Premiere Mesure du premier laser */
  mesurePrecedente=analogRead(recepteur1);
  
}

void loop() 
{
  detectObjetLaser1(&mesurePrecedente,&compteur);// mesure du capteur laser1

  if (compteur != 0) // Si un objet est sur le tapis
  { 
        stepper.runSpeed(); //Moteur tourne
      if(detectObjetLaser2()!=0) //Si il y a un d√©chet au niveau de la zone de d√©tection de couleur
      {  
       
        delayMicroseconds(1); // arr√™t du moteur

        while(detectColor()==0); //Tant que la couleur du d√©chet n'est pas reconnu au moins une fois
        
        detectObjetLaser1(&mesurePrecedente,&compteur);// mesure du capteur
        
        stepper.runSpeed();//Relance le moteur

        while(detectObjetLaser2()!=0); //Le moteur tourne tant que le d√©chet est dans la zone de d√©tection de couleur
        compteur--;//le compteur d√©cr√©mente

        delay(1000);//Arr√™t du moteur pendant 1 seconde pour permettre la prise du d√©chet
      }
      
  }

  
}

void configureVitesseMoteur()
{
stepper.setMaxSpeed(vitt); // Vitesse max en pas/seconde
stepper.setSpeed(vitt); //Vitesse du moteur en pas/s
}

```

**Code pour la d√©tection de couleur**

```cpp
//Fichier .cpp

#include <Arduino.h>
#include "DetectionCouleur.h"
#include <ArduinoJson.h> 
#include "LedRGB.h"

/*Fonction principale de detection des couleurs */
void detectColor()
{
  // Couleur pr√©c√©dente
  int couleurPrecedente = 4; 

  // Lecture Rouge
  float redFreq = lireFrequence(LOW, LOW);
  // Lecture Vert
  float greenFreq = lireFrequence(HIGH, HIGH);
  // Lecture Bleu
  float blueFreq = lireFrequence(LOW, HIGH);

  //Creation d'une variable de type Json
  JsonDocument doc;

  //Contraindre les valeurs √† rester dans la plage de fr√©quences d√©finies
  redFreq=constrain(redFreq,minR,maxR);
  greenFreq=constrain(greenFreq,minG,maxG);
  blueFreq=constrain(blueFreq,minB,maxB);

  //Correspondance de la plage de fr√©quences avec celle RGB etendue √† 1023
  int R = map(redFreq, minR, maxR, 1023, 0);
  int G = map(greenFreq,minG, maxG, 1023, 0);
  int B = map(blueFreq, minB, maxB, 1023, 0);

  //Determination de la nature de la couleur 
  int indexCouleur=compareCouleur(R,G,B);

if(indexCouleur!=4 && indexCouleur!=couleurPrecedente) //Couleur Detect√© et differente de la premiere 
{
      if(indexCouleur==0)//Rouge
      {
        doc["color_waste"]="red";
      }
      else if (indexCouleur==1)//Vert
      {
        doc["color_waste"]="green";
      }
      else if(indexCouleur==2)//Bleu
      {
        doc["color_waste"]="blue";
      }
      else //Jaune
      {
        doc["color_waste"]="yellow";
      }
      /* Envoie des donnees sous le format Json */
      serializeJson(doc, Serial);
      Serial.println();
}
// Mettre √† jour la couleur pr√©c√©dente
couleurPrecedente=indexCouleur;

/* Allumer la LED RGB dans la couleur correspondante */
afficherLED(indexCouleur);

}

/* Distance des valeurs mesurees aux valeurs de reference */
int couleurDistance(int R,int G,int B,int r1,int r2,int r3)
{
  return pow(r1-R,2)+pow(r2-G,2)+pow(r3-B,2);
}

/* Determination de la couleur la plus proche √† travers son indice */
int compareCouleur(int R, int G, int B)
{
  int i=0, distance=0,indexMin= 0;
  int minDistance=couleurDistance(refR[0],refV[0],refB[0],R,G,B); //calcul de la distance de la mesure actuelle √† la valeur de reference du rouge
  for (i=1;i<5;i++)
  {
    distance=couleurDistance(refR[i],refV[i],refB[i],R,G,B);
    if(minDistance>=distance)
    {
      minDistance=distance;
      indexMin=i;
    }
    
  }
  return indexMin;
}

/* Lecture de la frequence */
float lireFrequence(bool s2, bool s3) 
{
  digitalWrite(S2, s2);
  digitalWrite(S3, s3);
  delay(2); // petit d√©lai de stabilisation
  return pulseIn(sensorOut, LOW);
}

/* Configuration des broches */
void pinModeDetectCouleur()
{
  pinMode(S0, OUTPUT);
  pinMode(S1, OUTPUT);
  pinMode(S2, OUTPUT);
  pinMode(S3, OUTPUT);
  pinMode(sensorOut, INPUT);

}

/* Configurer le pourcentage de frequence */
 void frequence(bool s0,bool s1)
 {
  digitalWrite(S0, s0);
  digitalWrite(S1, s1);
 }

```

```cpp
//Fichier .h

#ifndef DETECTIONCOULEUR_H_INCLUDED
#define DETECTIONCOULEUR_H_INCLUDED

#define sensorOut 2 
#define S0 4  
#define S1 5
#define S2 6
#define S3 7

const int refR[5]={1023,0,0,1023, 0}; //Rouge
const int refV[5]={0,1023,0,1023, 0}; //Vert
const int refB[5]={0, 0,1023,0, 0}; //Bleu

// Valeurs mesur√©es durant l'√©talonnage
const float minR = 29;  // Frequence de Rouge dans du blanc pur
const float maxR = 230;  // Frequence de Rouge dans du noir pur

const float minG = 30; // Frequence de Vert dans du blanc pur
const float maxG = 290;// Frequence de Vert dans du noir pur

const float minB = 25; // Frequence de Bleu dans du blanc pur
const float maxB = 230; // Frequence de Bleu dans du noir pur

void detectColor();
int compareCouleur(int R, int G, int B);
int couleurDistance(int R,int G,int B,int r1,int r2,int r3);
void afficherLED(int n );
float lireFrequence(bool s2, bool s3);
void pinModeDetectCouleur();
 void frequence(bool s0,bool s1);
#endif // DETECTIONCOULEUR_H_INCLUDED

```

**Code pour la d√©tection sur le tapis**

```cpp
//Fichier .cpp
#include <Arduino.h>
#include "DetectionSurBande.h"

/* Allumage des lasers */
void allumageLaser()
{
digitalWrite(laser1, HIGH); 
digitalWrite(laser2, HIGH);
}

/*Configuration des broches en sortie */
void pinModeDetectSurBande()
{
  pinMode(laser1, OUTPUT);
  pinMode(laser2, OUTPUT);
}

/*Allumer et eteindre le buzzer */
void allumeEteintBuzzer()
{
  tone(BUZZER, 1000);
  delay(500);
  noTone(BUZZER);

}

/*Verification de la presence d'un nouveau dechet sur le tapis */
bool newObjectOnTape(int mesurePrecedente,int mesureActuelle)
{
if(mesurePrecedente-mesureActuelle>=100) return 1;// Il y a bien un nouvel objet sur le tapis
return 0;
}

/*Fonction de detection d'un dechet sur le tapis par le premier laser */
void detectObjetLaser1(int*mesurePrecedente, int* compteur)
{
  // mesure de la lumi√®re √† l'aide d'un capteur
  int mesureActu = analogRead(recepteur1);
  Serial.print("Capteur de lumi√®re: " + String(mesureActu));

  bool newObject=newObjectOnTape(*mesurePrecedente, mesureActu);
  if(mesureActu<=900 && newObject==1) //Nouvel Objet sur le tapis
  {   allumeEteintBuzzer();
      (*compteur)++;
  }
  *mesurePrecedente=mesureActu;
}

/*Fonction de detection d'un dechet sur le tapis par le second laser */
int detectObjetLaser2()
{
  // mesure de la lumi√®re √† l'aide d'un capteur
  int mesure = analogRead(recepteur2);
  Serial.print("Capteur de lumi√®re: " + String(mesure));

  if(mesure <=900 ) return 1; //Objet detect√© en fin du tapis
  return 0;
}

```

```cpp
//Fichier .h
#ifndef DETECTSURBANDE_H_INCLUDED
#define DETECTSURBANDE_H_INCLUDED

// broche de connexion du 1er module laser
#define laser1 8
//broche de connexion du 2e module laser   
#define laser2 13  
// broche de connexion buzzer
#define BUZZER 3      
// broche de connexion du r√©cepteur du laser1
#define recepteur1 A2 
// broche de connexion du r√©cepteur du laser2
#define recepteur2 A3 
const int seuil=900;

void allumageLaser();
void pinModeDetectSurBande();
void allumeEteintBuzzer();
bool newObjectOnTape(int mesurePrecedente,int mesureActuelle);
void detectObjetLaser1(int*mesurePrecedente, int* Compteur);
int detectObjetLaser2();

#endif // DETECTSURBANDE_H_INCLUDED

```

**Code pour la LED RGB**

```cpp
//Fichier .cpp
#include <Arduino.h>
#include "LedRGB.h"

/* Configuration des pins de la LED en sortie */
void pinModeLedRgb()
{
  pinMode(ledRouge,OUTPUT);
  pinMode(ledVert, OUTPUT);
  pinMode(ledBleu, OUTPUT);
}

/* Allumage de la LED RGB dans la couleur detect√©e */
void afficherLED(int n)
{
  if (n==0) //Rouge 
  { analogWrite(ledRouge, 255);
    analogWrite(ledVert, 0);
    analogWrite(ledBleu, 0);
  }
  else if(n==1)//Vert
  {
    analogWrite(ledRouge, 0);
    analogWrite(ledVert, 255);
    analogWrite(ledBleu, 0);
  }
  else if(n==2)// Bleu
  {
    analogWrite(ledRouge, 0);
    analogWrite(ledVert, 0);
    analogWrite(ledBleu, 255);
  }
  else if(n==3)// Jaune
  {
    analogWrite(ledRouge, 255);
    analogWrite(ledVert, 255);
    analogWrite(ledBleu, 0); 
  }
  else //Aucune couleur
  {
  analogWrite(ledRouge, 0);
  analogWrite(ledVert, 0);
  analogWrite(ledBleu, 0); 
  }
}

```

```cpp
//Fichier .h
#ifndef LEDRGB_H_INCLUDED
#define LEDRGB_H_INCLUDED

// broche de connexion du Rouge de la led RGB
#define ledRouge 9  
// broche de connexion du Bleu de la led RGB
#define ledBleu 10  
// broche de connexion du Rouge de la led RGB
#define ledVert  11 

void afficherLED(int n);
void pinModeLedRgb();
#endif // LEDRGB_H_INCLUDED
```